function Merge leftList as list, rightList as list
	local mergedList as list
	repeat while leftList is not empty and rightList is not empty
		if first element of leftList <= first element of rightList then
			put first element of leftList after mergedList
			delete first element of leftList
		else
			put first element of rightList after mergedList
			delete first element of rightList
		end if
	end repeat
	repeat while leftList is not empty
		put first element of leftList after mergedList
		delete first element of leftList
	end repeat
	repeat while rightList is not empty
		put first element of rightList after mergedList
		delete first element of rightList
	end repeat
	return mergedList
end Merge

function MergeSort unsortedList as list
	if unsortedList is empty then return unsortedList
	if the number of elements in unsortedList is one then return unsortedList
	local middleElement as integer is (the number of elements in unsortedList) div 2
	local leftList as list is MergeSort(elements 1 through middleElement of unsortedList)
	local rightList as list is MergeSort(elements middleElement+1 through -1 of unsortedList)
	return Merge(leftList, rightList)
end MergeSort

local x as list is (5, 9, 3.14, 12, 0, -4, 18, -7.7, 1, 36, 2.4, 42.01, -99)
put MergeSort(x)
sort elements of x numeric
put x
