on graph y as string is "sqrt(x)", leftWindow as number is -10, rightWindow as number is 10, ¬
		topWindow as number is 5, bottomWindow as number is -5, ¬
		columnsAvailable as integer is 80, rowsAvailable as integer is 20
	local theGraph as string is empty
	
	repeat with j = 1 to rowsAvailable
		put rpad("",columnsAvailable) into line j of theGraph
		
		put round( 1 + (0-leftWindow) * columnsAvailable / (rightWindow-leftWindow) ) into i
		if not (i < 1 or i > columnsAvailable or (not (i=i))) then
			put "|" into char i of line j of theGraph
		end if
	end repeat
	
	put round( 1 + (0-topWindow) * rowsAvailable / (bottomWindow-topWindow) ) into j
	if not (j < 1 or j > rowsAvailable or (not (j=j))) then
		put rpad("",columnsAvailable,"-") into line j of theGraph
	end if
	
	repeat with i = 1 to columnsAvailable
		put leftWindow + (i-1) * (rightWindow-leftWindow) / (columnsAvailable-1) into xx
		
		put xx into x
		put the value of y into yy
		
		put round( 1 + (yy-topWindow) * rowsAvailable / (bottomWindow-topWindow) ) into j
		if j < 1 or j > rowsAvailable or (not (j=j)) then next repeat
		else put "*" into char i of line j of theGraph
	end repeat
	
	if theGraph ends with the lineEnding then delete char -(the length of the lineEnding) to -1 of theGraph
	put theGraph
end graph

put "sqrt(x)"
graph "sqrt(x)"
put "x^3-x"
graph "x^3-x"
put "sin(x)"
graph "sin(x)"
put "tan(x)"
graph "tan(x)"
put "atan(x)"
graph "atan(x)"
put "cos(x)/x"
graph "cos(x)/x"
put "1/x"
graph "1/x"
put "sinh(x)"
graph "sinh(x)"
put "cosh(x)"
graph "cosh(x)"
put "tanh(x) "
graph "tanh(x) "
put "atanh(x)"
graph "atanh(x)"
put "asin(x)"
graph "asin(x)"
put "acos(x)"
graph "acos(x)"
